#vars $grad_type : for rev none.

% Base node for all nominals, used for completing case endings
NOMINAL:
	% Only plural queries end up here
	<$grad_type pl nom> == "<sg_stem Gradation:<$grad_type nom pl>>" t
	
	<$grad_type pl part a> == "<pl_stem Gradation:<$grad_type part pl a>>" Harmony:<"<harmony>" a>
	<$grad_type pl part ta> == "<pl_stem Gradation:<$grad_type part pl ta>>" t Harmony:<"<harmony>" a>
	
	% Only singular queries end up here
	<$grad_type sg gen> == "<sg_stem Gradation:<$grad_type gen sg>>" n
	% Sentences for the genitive plural
	<$grad_type pl gen en> == "<pl_stem Gradation:<$grad_type gen pl en>>" en
	<$grad_type pl gen den> == "<pl_stem Gradation:<$grad_type gen pl den>>" den
	% Debatable
	<$grad_type sg ill vn> == "<sg_stem Gradation:<$grad_type ill sg vn>>" "<final vowel>" n
	<$grad_type sg ill hvn> == "<sg_stem Gradation:<$grad_type ill sg hvn>>" h "<final vowel>" n
	<$grad_type sg ill seen> == "<sg_stem Gradation:<$grad_type ill sg seen>>" seen
	
	<$grad_type pl ill in> == "<pl_stem Gradation:<$grad_type ill pl in>>" in
	<$grad_type pl ill hin> == "<pl_stem Gradation:<$grad_type ill pl hin>>" hin
	<$grad_type pl ill siin> == "<pl_stem Gradation:<$grad_type ill pl siin>>" siin
	
	% Essive always in stong form
	<$grad_type sg ess> == "<sg_stem Gradation:<$grad_type ess sg>>" n Harmony:<"<harmony>" a>
	<$grad_type pl ess> == "<pl_stem Gradation:<$grad_type ess pl>>" n Harmony:<"<harmony>" a>
	
	% "Regular" cases
	<$grad_type sg iness> == "<sg_stem Gradation:<$grad_type iness sg>>" ss Harmony:<"<harmony>" a>
	<$grad_type pl iness> == "<pl_stem Gradation:<$grad_type iness pl>>" ss Harmony:<"<harmony>" a>
	<$grad_type sg ela> == "<sg_stem Gradation:<$grad_type ela sg>>" st Harmony:<"<harmony>" a>
	<$grad_type pl ela> == "<pl_stem Gradation:<$grad_type ela pl>>" st Harmony:<"<harmony>" a>
	<$grad_type sg all> == "<sg_stem Gradation:<$grad_type all sg>>" lle
	<$grad_type pl all> == "<pl_stem Gradation:<$grad_type all pl>>" lle
	<$grad_type sg adess> == "<sg_stem Gradation:<$grad_type adess sg>>" ll Harmony:<"<harmony>" a>
	<$grad_type pl adess> == "<pl_stem Gradation:<$grad_type adess pl>>" ll Harmony:<"<harmony>" a>
	<$grad_type sg abl> == "<sg_stem Gradation:<$grad_type abl sg>>" lt Harmony:<"<harmony>" a>
	<$grad_type pl abl> == "<pl_stem Gradation:<$grad_type abl pl>>" lt Harmony:<"<harmony>" a>
	
	<$grad_type sg trans> == "<sg_stem Gradation:<$grad_type trans sg>>" ksi
	<$grad_type pl trans> == "<pl_stem Gradation:<$grad_type trans pl>>" ksi.

Harmony:
	<front a> == ä
	<front o> == ö
	<front u> == y
	<back a> == a
	<back o> == o
	<back u> == u.

Gradation_Res:
	<pp strong> == pp
	<pp weak> == p
	<tt strong> == tt
	<tt weak> == t
	<kk strong> == kk
	<kk weak> == k
	
	<mp strong> == mp
	<mp weak> == mm
	<nt strong> == nt
	<nt weak> == nn
	<lt strong> == lt
	<lt weak> == ll
	<rt strong> == rt
	<rt weak> == rr
	<nk strong> == nk
	<nk weak> == ng
	
	<p strong> == p
	<p weak> == v
	<t strong> == t
	<t weak> == d
	<k strong> == k
	<k weak> == .

Gradation:
	<none> == none

	<for> == weak
	<for nom sg> == strong
	<for part> == strong
	<for gen pl en> == strong
	<for part pl ta> == weak
	<for ill> == strong
	<for ess> == strong

	<rev> == strong
	<rev nom sg> == weak
	<rev part sg> == weak.

% Declension types based on Kotimaisten kielten keskus

% Ending in ⟨-o⟩, ⟨-u⟩, ⟨-ö⟩, ⟨-y⟩, 2 syllables, and past passive participles with any number of syllables, With final-syllable gradation.
% valo
% 18340
Type1:
	<gt> == for
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>"
	<sg_stem> == "<stem>"
	<sg part> == <sg nom> Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" i
	<pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<pl gen> == NOMINAL:<"<gt>" pl gen en>
	<pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-o⟩, ⟨-u⟩, ⟨-ö⟩, ⟨-y⟩, 3+ syllables, No gradation.
% palvelu
% 4610
Type2:
	<gt> == none
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>"
	<sg_stem> == "<stem>"
	<sg part> == <sg nom> Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" i
	<pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<pl gen> == NOMINAL:<"<gt>" pl gen en> xxx NOMINAL:<"<gt>" pl gen den>
	<pl part> == NOMINAL:<"<gt>" pl part a> xxx NOMINAL:<"<gt>" pl part ta>.

% Ending in ⟨-ao⟩, ⟨-eo⟩, ⟨-io⟩, ⟨-iö⟩, ⟨-ie⟩, ⟨-oe⟩, No gradation.
% valtio
% 2640
Type3:
	<gt> == none
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>"
	<sg_stem> == "<stem>"
	<sg part> == "<stem Gradation:<"<gt>" part sg>>" t Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" i
	<pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<pl gen> == NOMINAL:<"<gt>" pl gen den>
	<pl part> == NOMINAL:<"<gt>" pl part ta>.

% Ending in ⟨-kko⟩, ⟨-kkö⟩, ⟨-tto⟩, 3+ syllables, With final-syllable gradation.
% laatikko
% 1094
Type4:
	<gt> == for
	<final vowel> == o
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>"
	<sg_stem> == "<stem>"
	<sg part> == <sg nom> Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" i
	<pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<pl gen> == NOMINAL:<"<gt>" pl gen en> xxx NOMINAL:<"<gt>" pl gen den>
	<pl part> == NOMINAL:<"<gt>" pl part a> xxx NOMINAL:<"<gt>" pl part ta>.

% Ending in ⟨-i⟩ (possibly no vowel in the nominative singular), 2 syllables, With final-syllable gradation.
% risti
% 22258
Type5:
	<gt> == for
	<final vowel> == i
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" i
	<sg_stem> == "<stem>" i
	<sg part> == <sg nom> Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" ei
	<pl ill> == NOMINAL:<"<gt>" pl ill hin>
	% Extra rule for manual genitive based on nominative
	<pl gen> == <sg nom> en
	<pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-i⟩ (possibly no vowel in the nominative singular), 3+ syllables, No gradation.
% paperi
% 5752
Type6:
	<gt> == none
	<final vowel> == i
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" i
	<sg_stem> == "<stem>" i
	<sg part> == <sg nom> Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" ei
	<pl ill> == NOMINAL:<"<gt>" pl ill hin>
	% Extra rule for manual genitive based on nominative
	<pl gen> == <sg nom> en xxx NOMINAL:<"<gt>" pl gen den>
	<pl part> == NOMINAL:<"<gt>" pl part a> xxx NOMINAL:<"<gt>" pl part ta>.

% Ending in ⟨-e⟩ (⟨-e⟩ > ⟨-i⟩ in the nominative singular), 2 syllables, With final-syllable gradation.
% ovi
% 2515
Type7:
	<gt> == for
	<final vowel> == e
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" i
	<sg_stem> == "<stem>" e
	<sg part> == "<stem Gradation:<"<gt>" part sg>>" e Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" i
	<pl ill> == NOMINAL:<"<gt>" pl ill in>
	<pl gen> == NOMINAL:<"<gt>" pl gen en>
	<pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-e⟩, 2 syllables, With final-syllable gradation.
% nalle
% 391
Type8:
	<gt> == for
	<final vowel> == e
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>"
	<sg_stem> == "<stem>"
	<sg part> == <sg nom> Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" i
	<pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<pl gen> == NOMINAL:<"<gt>" pl gen en>
	<pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-a⟩, 2 syllables, Non-rounded vowel (a, e, i) in first syllable, With final-syllable gradation.
% kala
% 11733
Type9:
	<gt> == for
	<final vowel> == Harmony:<"<harmony>" a>
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" Harmony:<"<harmony>" a>
	<sg_stem> == "<stem>" Harmony:<"<harmony>" a>
	<sg part> == <sg nom> Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" Harmony:<"<harmony>" o> i
	<pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<pl gen> == NOMINAL:<"<gt>" pl gen en>
	<pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-a⟩ or ⟨-ä⟩, 2 syllables, If ending in ⟨-a⟩, rounded vowel (⟨o⟩ or ⟨u⟩) in first syllable, With final-syllable gradation.
% koira
% 15597
Type10:
	<gt> == for
	<final vowel> == Harmony:<"<harmony>" a>
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" Harmony:<"<harmony>" a>
	<sg_stem> == "<stem>" Harmony:<"<harmony>" a>
	<sg part> == <sg nom> Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" i
	<pl ill> == NOMINAL:<"<gt>" pl ill in>
	<pl gen> == NOMINAL:<"<gt>" pl gen en>
	<pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in short consonant + ⟨-a⟩ or ⟨-ä⟩, 3+ syllables. No gradation.
% omena
% 165
Type11:
	<gt> == none
	<final vowel> == Harmony:<"<harmony>" a>
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" Harmony:<"<harmony>" a>
	<sg_stem> == "<stem>" Harmony:<"<harmony>" a>
	<sg part> == <sg nom> Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" Harmony:<"<harmony>" o> i
	<pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<pl gen> == NOMINAL:<"<gt>" pl gen den>
	<pl part> == NOMINAL:<"<gt>" pl part ta>.

% Ending in ⟨-a⟩ or ⟨-ä⟩, 3+ syllables. No gradation.
% kulkija
% 6726
Type12:
	<gt> == none
	<final vowel> == Harmony:<"<harmony>" a>
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" Harmony:<"<harmony>" a>
	<sg_stem> == "<stem>" Harmony:<"<harmony>" a>
	<sg part> == <sg nom> Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" Harmony:<"<harmony>" o> i
	<pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<pl gen> == NOMINAL:<"<gt>" pl gen den>
	<pl part> == NOMINAL:<"<gt>" pl part ta>.

% Ending in ⟨-a⟩ or ⟨-ä⟩, 3+ syllables. No gradation.
% katsika
% 519
Type13:
	<gt> == none
	<final vowel> == Harmony:<"<harmony>" a>
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" Harmony:<"<harmony>" a>
	<sg_stem> == "<stem>" Harmony:<"<harmony>" a>
	<sg part> == <sg nom> Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" Harmony:<"<harmony>" o> i
	<pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<pl gen> == NOMINAL:<"<gt>" pl gen en> xxx NOMINAL:<"<gt>" pl gen den>
	<pl part> == NOMINAL:<"<gt>" pl part a> xxx NOMINAL:<"<gt>" pl part ta>.

% Ending in long consonant + ⟨-a⟩ or ⟨-ä⟩, 3+ syllables. With final-syllable gradation.
% solakka
% 1081
Type14:
	<gt> == for
	<final vowel> == Harmony:<"<harmony>" a>
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" Harmony:<"<harmony>" a>
	<sg_stem> == "<stem>" Harmony:<"<harmony>" a>
	<sg part> == <sg nom> Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" Harmony:<"<harmony>" o> i
	<pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<pl gen> == NOMINAL:<"<gt>" pl gen en> xxx NOMINAL:<"<gt>" pl gen den>
	<pl part> == NOMINAL:<"<gt>" pl part a> xxx NOMINAL:<"<gt>" pl part ta>.

% Ending in ⟨-ea⟩, ⟨-eä⟩, ⟨-oa⟩. No gradation.
% korkea
% 410
Type15:
	<gt> == none
	<final vowel> == Harmony:<"<harmony>" a>
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" Harmony:<"<harmony>" a>
	<sg_stem> == "<stem>" Harmony:<"<harmony>" a>
	<sg part> == <sg nom> Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" i
	<pl ill> == NOMINAL:<"<gt>" pl ill hin> xxx NOMINAL:<"<gt>" pl ill siin>
	<pl gen> == NOMINAL:<"<gt>" pl gen den>
	<pl part> == NOMINAL:<"<gt>" pl part ta>.

% Comparative adjectives. Ending in ⟨-mpa⟩, ⟨-mpä⟩ (⟨-a⟩or⟨-ä⟩ > ⟨-i⟩ in the nominative singular). With final-syllable gradation.
% vanhempi
% 500
Type16:
	<gt> == for
	<final vowel> == Harmony:<"<harmony>" a>
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" i
	<sg_stem> == "<stem>" Harmony:<"<harmony>" a>
	<sg part> == "<stem Gradation:<"<gt>" nom sg>>" Harmony:<"<harmony>" a> Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" i
	<pl ill> == NOMINAL:<"<gt>" pl ill in>
	<pl gen> == NOMINAL:<"<gt>" pl gen en>
	<pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-aa⟩, ⟨-oo⟩, ⟨-uu⟩, rarely ⟨-ee⟩, ⟨-yy⟩, 2+ syllables. No gradation.
% vapaa
% 276
Type17:
	<gt> == none
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" "<final vowel>"
	<sg_stem> == "<stem>" "<final vowel>"
	<sg part> == <sg nom> t Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill seen>
	
	<pl_stem> == "<stem>" i
	<pl ill> == NOMINAL:<"<gt>" pl ill siin>
	<pl gen> == NOMINAL:<"<gt>" pl gen den>
	<pl part> == NOMINAL:<"<gt>" pl part ta>.

% Ending in a long vowel xxx a diphthong ending in ⟨-i⟩, 1 syllable. No gradation.
% maa
% 1464
Type18:
	<gt> == none
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" "<final vowel>"
	<sg_stem> == "<stem>" "<final vowel>"
	<sg part> == <sg nom> t Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill hvn>
	
	<pl_stem> == "<stem>" i
	<pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<pl gen> == NOMINAL:<"<gt>" pl gen den>
	<pl part> == NOMINAL:<"<gt>" pl part ta>.

% Ending in ⟨-ie⟩, ⟨-uo⟩, ⟨-yö⟩, 1 syllable. No gradation.
% suo
% 662
Type19ie:
	<gt> == none
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" ie
	<sg_stem> == "<stem>" ie
	<sg part> == <sg nom> t Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill hvn>
	
	<pl_stem> == "<stem>" ei
	<pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<pl gen> == NOMINAL:<"<gt>" pl gen den>
	<pl part> == NOMINAL:<"<gt>" pl part ta>.

Type19uo:
	<gt> == none
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" Harmony:<"<harmony>" u> Harmony:<"<harmony>" o>
	<sg_stem> == "<stem>" Harmony:<"<harmony>" u> Harmony:<"<harmony>" o>
	<sg part> == <sg nom> t Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill hvn>
	
	<pl_stem> == "<stem>" Harmony:<"<harmony>" o> i
	<pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<pl gen> == NOMINAL:<"<gt>" pl gen den>
	<pl part> == NOMINAL:<"<gt>" pl part ta>.

% Ending in ⟨-ee⟩, ⟨-öö⟩, ⟨-yy⟩, rarely ⟨-aa⟩, ⟨-oo⟩, ⟨-uu⟩, 2+ syllables. No gradation.
% filee
% 145
Type20:
	<gt> == none
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" "<final vowel>"
	<sg_stem> == "<stem>" "<final vowel>"
	<sg part> == <sg nom> t Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill hvn> xxx NOMINAL:<"<gt>" sg ill seen>
	
	<pl_stem> == "<stem>" i
	<pl ill> == NOMINAL:<"<gt>" pl ill hin> xxx NOMINAL:<"<gt>" pl ill siin>
	<pl gen> == NOMINAL:<"<gt>" pl gen den>
	<pl part> == NOMINAL:<"<gt>" pl part ta>.

% Foreign words ending in a vowel. No gradation.
% rosé
% 59
Type21:
	<gt> == none
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>"
	<sg_stem> == "<stem>"
	<sg part> == "<stem Gradation:<"<gt>" part sg>>" t Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill hvn>
	
	<pl_stem> == "<stem>" i
	<pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<pl gen> == NOMINAL:<"<gt>" pl gen den>
	<pl part> == NOMINAL:<"<gt>" pl part ta>.

% Foreign words ending in a vowel, but written with a final consonant (mostly French loanwords). No gradation.
% parfait
% 22
Type22:
	<gt> == none
	<final vowel> == e
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>"
	<sg_stem> == "<stem>" ’
	<sg part> == "<stem Gradation:<"<gt>" part sg>>" t Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill hvn>
	
	<pl_stem> == "<stem>" ’i
	<pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<pl gen> == NOMINAL:<"<gt>" pl gen den>
	<pl part> == NOMINAL:<"<gt>" pl part ta>.

% Ending in ⟨-he-⟩, ⟨-le-⟩, rarely ⟨-ne-⟩ (⟨-e⟩ > ⟨-i⟩ in the nominative singular), 2 syllables. No gradation.
% tiili
% 60
Type23:
	<gt> == none
	<final vowel> == e
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" i
	<sg_stem> == "<stem>" e
	<sg part> == "<stem Gradation:<"<gt>" part sg>>" t Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" i
	<pl ill> == NOMINAL:<"<gt>" pl ill in>
	<pl gen> == NOMINAL:<"<gt>" pl gen en>
	<pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-he-⟩, ⟨-le-⟩, ⟨-ne-⟩, ⟨-re-⟩, ⟨-se-⟩ (⟨-e⟩ > ⟨-i⟩ in the nominative singular), 2 syllables. No gradation.
% uni
% 412
Type24:
	<gt> == none
	<final vowel> == e
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" i
	<sg_stem> == "<stem>" e
	<sg part> == "<stem Gradation:<"<gt>" part sg>>" t Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" i
	<pl ill> == NOMINAL:<"<gt>" pl ill in>
	<pl gen> == NOMINAL:<"<gt>" pl gen en> xxx "<stem Gradation:<"<gt>" gen pl>>" ten
	<pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-me-⟩ (⟨-e⟩ > ⟨-i⟩ in the nominative singular), 2 syllables. No gradation.
% toimi
% 299
Type25:
	<gt> == none
	<final vowel> == e
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" mi
	<sg_stem> == "<stem>" me
	<sg part> == "<stem Gradation:<"<gt>" part sg>>" nt Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" mi
	<pl ill> == NOMINAL:<"<gt>" pl ill in>
	<pl gen> == NOMINAL:<"<gt>" pl gen en> xxx "<stem Gradation:<"<gt>" gen pl>>" nten
	<pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-le-⟩, ⟨-ne-⟩, ⟨-re-⟩ (⟨-e⟩ > ⟨-i⟩ in the nominative singular), 2 syllables. No gradation.
% pieni
% 1008
Type26:
	<gt> == none
	<final vowel> == e
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" i
	<sg_stem> == "<stem>" e
	<sg part> == "<stem Gradation:<"<gt>" part sg>>" t Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" i
	<pl ill> == NOMINAL:<"<gt>" pl ill in>
	<pl gen> == NOMINAL:<"<gt>" pl gen en> xxx "<stem Gradation:<"<gt>" gen pl>>" ten
	<pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-te-⟩ (⟨-te⟩ > ⟨-si⟩ in the nominative singular), 2 syllables. With final-syllable gradation.
% käsi
% 612
Type27:
	<gt> == for
	<final vowel> == e
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" si
	<sg_stem> == "<stem>" Gradation_Res:<t> e
	<sg part> == "<stem Gradation:<"<gt>" part sg>>" tt Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" si
	<pl ill> == NOMINAL:<"<gt>" pl ill in>
	<pl gen> == NOMINAL:<"<gt>" pl gen en> xxx "<stem Gradation:<"<gt>" gen pl>>" tten
	<pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-lte-⟩, ⟨-nte-⟩, ⟨-rte-⟩ (⟨-te⟩ > ⟨-si⟩ in the nominative singular), 2 syllables. With final-syllable gradation.
% kynsi
% 208
Type28nt:
	<gt> == for
	<final vowel> == e
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" nsi
	<sg_stem> == "<stem>" Gradation_Res:<nt> e
	<sg part> == "<stem Gradation:<"<gt>" part sg>>" ntt Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" nsi
	<pl ill> == NOMINAL:<"<gt>" pl ill in>
	<pl gen> == NOMINAL:<"<gt>" pl gen en> xxx "<stem Gradation:<"<gt>" gen pl>>" ntten
	<pl part> == NOMINAL:<"<gt>" pl part a>.

Type28rt:
	<gt> == for
	<final vowel> == e
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" rsi
	<sg_stem> == "<stem>" Gradation_Res:<rt> e
	<sg part> == "<stem Gradation:<"<gt>" part sg>>" rtt Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" rsi
	<pl ill> == NOMINAL:<"<gt>" pl ill in>
	<pl gen> == NOMINAL:<"<gt>" pl gen en> xxx "<stem Gradation:<"<gt>" gen pl>>" rtten
	<pl part> == NOMINAL:<"<gt>" pl part a>.

Type28lt:
	<gt> == for
	<final vowel> == e
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" lsi
	<sg_stem> == "<stem>" Gradation_Res:<lt> e
	<sg part> == "<stem Gradation:<"<gt>" part sg>>" ltt Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" lsi
	<pl ill> == NOMINAL:<"<gt>" pl ill in>
	<pl gen> == NOMINAL:<"<gt>" pl gen en> xxx "<stem Gradation:<"<gt>" gen pl>>" ltten
	<pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-pse-⟩, ⟨-kse-⟩ (⟨-e⟩ > ⟨-i⟩ in the nominative singular), 2 syllables. No gradation.
% lapsi
% 70
Type29:
	<gt> == none
	<final vowel> == e
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" psi
	<sg_stem> == "<stem>" pse
	<sg part> == "<stem Gradation:<"<gt>" part sg>>" st Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" psi
	<pl ill> == NOMINAL:<"<gt>" pl ill in>
	<pl gen> == NOMINAL:<"<gt>" pl gen en> xxx "<stem Gradation:<"<gt>" gen pl>>" sten
	<pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-tse-⟩ (⟨-e⟩ > ⟨-i⟩ in the nominative singular), 2 syllables. No gradation.
% veitsi
% 55
Type30:
	<gt> == none
	<final vowel> == e
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" tsi
	<sg_stem> == "<stem>" tse
	<sg part> == "<stem Gradation:<"<gt>" part sg>>" st Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" tsi
	<pl ill> == NOMINAL:<"<gt>" pl ill in>
	<pl gen> == NOMINAL:<"<gt>" pl gen en> xxx "<stem Gradation:<"<gt>" gen pl>>" sten
	<pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-hte-⟩ (⟨-hte⟩ > ⟨-ksi⟩ in the nominative singular), 2 syllables. With final-syllable gradation.
% kaksi
% 16
Type31:
	<gt> == for
	<final vowel> == e
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" ksi
	<sg_stem> == "<stem>" h Gradation_Res:<t> e
	<sg part> == "<stem Gradation:<"<gt>" part sg>>" ht Harmony:<"<harmony>" a>
	
	<pl_stem> == "<stem>" ksi
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	<pl ill> == NOMINAL:<"<gt>" pl ill in>
	<pl gen> == NOMINAL:<"<gt>" pl gen en>
	<pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-le-⟩, ⟨-ne-⟩, ⟨-re-⟩ (no vowel in the nominative singular), 3+ syllables. With penultimate-syllable gradation.➡️
% sisar
% 373
Type32:
	<gt> == rev
	<final vowel> == e
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>"
	<sg_stem> == "<stem>" e
	<sg part> == "<stem Gradation:<"<gt>" part sg>>" t Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" i
	<pl ill> == NOMINAL:<"<gt>" pl ill in>
	<pl gen> == NOMINAL:<"<gt>" pl gen en> xxx <sg nom> ten
	<pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-me-⟩ (⟨-me⟩ > ⟨-n⟩ in the nominative singular), 3+ syllables. With penultimate-syllable gradation.➡️
% kytkin
% 1884
Type33:
	<gt> == rev
	<final vowel> == e
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" n
	<sg_stem> == "<stem>" me
	<sg part> == <sg nom> t Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" mi
	<pl ill> == NOMINAL:<"<gt>" pl ill in>
	<pl gen> == NOMINAL:<"<gt>" pl gen en> xxx <sg nom> ten
	<pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-toma-⟩, ⟨-tömä-⟩ (⟨-ma⟩or⟨-mä⟩ > ⟨-n⟩ in the nominative singular), 3+ syllables. With penultimate-syllable gradation.➡️
% onneton
% 1124
Type34:
	<gt> == rev
	<final vowel> == Harmony:<"<harmony>" a>
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" n
	<sg_stem> == "<stem>" m Harmony:<"<harmony>" a>
	<sg part> == <sg nom> t Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" mi
	<pl ill> == NOMINAL:<"<gt>" pl ill in>
	<pl gen> == NOMINAL:<"<gt>" pl gen en>
	<pl part> == NOMINAL:<"<gt>" pl part a>.

% The words lämmin and hapan only. Ending in ⟨-mä-⟩ (⟨-mä⟩ > ⟨-n⟩ in the nominative singular), 3 syllables. With penultimate-syllable gradation.➡️
% lämmin, hapan
% 12
Type35:
	<gt> == rev
	<final vowel> == Harmony:<"<harmony>" a>
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" n
	<sg_stem> == "<stem>" m Harmony:<"<harmony>" a>
	<sg part> == <sg nom> t Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" mi
	<pl ill> == NOMINAL:<"<gt>" pl ill in>
	<pl gen> == NOMINAL:<"<gt>" pl gen en>
	<pl part> == NOMINAL:<"<gt>" pl part a>.

% Superlative adjectives. Ending in ⟨-impa-⟩, ⟨-impä-⟩ (⟨-mpa⟩or⟨-mpä⟩ > ⟨-n⟩ in the nominative singular), 2+ syllables. With penultimate-syllable gradation.➡️
% sisin
% 453
Type36:
	<gt> == for
	<final vowel> == Harmony:<"<harmony>" a>
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" n
	<sg_stem> == "<stem>" Gradation_Res:<mp> Harmony:<"<harmony>" a>
	<sg part> == "<stem Gradation:<"<gt>" part sg>>" nt Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" Gradation_Res:<mp> i
	<pl ill> == NOMINAL:<"<gt>" pl ill in>
	<pl gen> == NOMINAL:<"<gt>" pl gen en> xxx <sg nom> ten
	<pl part> == NOMINAL:<"<gt>" pl part a>.

% The word vasen only. Ending in ⟨-mpa-⟩ (⟨-mpa⟩ > ⟨-n⟩ in the nominative singular), 3 syllables. With penultimate-syllable gradation.➡️
% vasen
% 1
Type37:
	<gt> == for
	<final vowel> == Harmony:<"<harmony>" a>
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" n
	<sg_stem> == "<stem>" Gradation_Res:<mp> Harmony:<"<harmony>" a>
	<sg part> == "<stem Gradation:<"<gt>" part sg>>" nt Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" Gradation_Res:<mp> i
	<pl ill> == NOMINAL:<"<gt>" pl ill in>
	<pl gen> == NOMINAL:<"<gt>" pl gen en> xxx <sg nom> ten
	<pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-se-⟩ (⟨-se⟩ > ⟨-nen⟩ in the nominative singular), 2+ syllables. No gradation.
% nainen
% 19627
Type38:
	<gt> == none
	<final vowel> == e
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" nen
	<sg_stem> == "<stem>" se
	<sg part> == "<stem Gradation:<"<gt>" part sg>>" s t Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" si
	<pl ill> == NOMINAL:<"<gt>" pl ill in>
	<pl gen> == NOMINAL:<"<gt>" pl gen en> xxx "<stem Gradation:<"<gt>" gen pl>>" sten
	<pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-kse-⟩ (⟨-kse⟩ > ⟨-s⟩ in the nominative singular), 3+ syllables. No gradation.
% vastaus
% 10739
Type39:
	<gt> == none
	<final vowel> == e
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" s
	<sg_stem> == "<stem>" kse
	<sg part> == <sg nom> t Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" ksi
	<pl ill> == NOMINAL:<"<gt>" pl ill in>
	<pl gen> == NOMINAL:<"<gt>" pl gen en> xxx <sg nom> ten
	<pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-te-⟩ (⟨-te⟩ > ⟨-s⟩ in the nominative singular), 3+ syllables. With final-syllable gradation.
% kalleus
% 6219
Type40:
	<gt> == for
	<final vowel> == e
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" s
	<sg_stem> == "<stem>" Gradation_Res:<t> e
	<sg part> == "<stem Gradation:<"<gt>" part sg>>" tt Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" ksi
	<pl ill> == NOMINAL:<"<gt>" pl ill in>
	<pl gen> == NOMINAL:<"<gt>" pl gen en>
	<pl part> == NOMINAL:<"<gt>" pl part a>.
	
% Ending in long vowel (long vowel > short vowel + ⟨-s⟩ in the nominative singular), 2+ syllables. With penultimate-syllable gradation.➡️
% vieras
% 2104
Type41:
	<gt> == rev
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" s
	<sg_stem> == "<stem>" "<final vowel>"
	<sg part> == <sg nom> t Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill seen>
	
	<pl_stem> == "<stem>" i
	<pl ill> == NOMINAL:<"<gt>" pl ill siin>
	<pl gen> == NOMINAL:<"<gt>" pl gen den>
	<pl part> == NOMINAL:<"<gt>" pl part ta>.

% The word mies only. Ending in ⟨-he-⟩ (⟨-he⟩ > ⟨-s⟩ in the nominative singular), 2 syllables. No gradation.
% mies
% 393
Type42:
	<gt> == none
	<final vowel> == e
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" s
	<sg_stem> == "<stem>" he
	<sg part> == "<stem Gradation:<"<gt>" part sg>>" st Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" hi
	<pl ill> == NOMINAL:<"<gt>" pl ill in>
	<pl gen> == NOMINAL:<"<gt>" pl gen en> xxx <sg nom> ten
	<pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-ue-⟩, ⟨-ye-⟩ (⟨-e⟩ > ⟨-t⟩ in the nominative singular), 3+ syllables. With penultimate-syllable gradation.➡️
% ohut
% 66
Type43:
	<gt> == rev
	<final vowel> == e
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" t
	<sg_stem> == "<stem>" e
	<sg part> == <sg nom> t Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" i
	<pl ill> == NOMINAL:<"<gt>" pl ill hin> xxx NOMINAL:<"<gt>" pl ill siin>
	<pl gen> == NOMINAL:<"<gt>" pl gen den>
	<pl part> == NOMINAL:<"<gt>" pl part ta>.

% Ending in ⟨-ää-⟩ (⟨-ä⟩ > ⟨-t⟩ in the nominative singular), 2+ syllables. No gradation (outside of proper nouns).
% kevät
% 8
Type44:
	<gt> == none
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" t
	<sg_stem> == "<stem>" Harmony:<"<harmony>" a>
	<sg part> == <sg nom> t Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill seen>
	
	<pl_stem> == "<stem>" i
	<pl ill> == NOMINAL:<"<gt>" pl ill hin> xxx NOMINAL:<"<gt>" pl ill siin>
	<pl gen> == NOMINAL:<"<gt>" pl gen den>
	<pl part> == NOMINAL:<"<gt>" pl part ta>.

% Ordinal numbers. Ending in ⟨-nte-⟩ (⟨-nte⟩ > ⟨-s⟩ in the nominative singular), 3+ syllables. With final-syllable gradation.
% kahdeksas
% 38
Type45:
	<gt> == for
	<final vowel> == e
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" s
	<sg_stem> == "<stem>" Gradation_Res:<nt> e
	<sg part> == "<stem Gradation:<"<gt>" part sg>>" tt Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" nsi
	<pl ill> == NOMINAL:<"<gt>" pl ill in>
	<pl gen> == NOMINAL:<"<gt>" pl gen en>
	<pl part> == NOMINAL:<"<gt>" pl part a>.

% The word tuhat only. Ending in ⟨-nte-⟩ (⟨-nte⟩ > ⟨-t⟩ in the nominative singular), 3 syllables. With final-syllable gradation.
% tuhat
% 2
Type46:
	<gt> == for
	<final vowel> == e
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" t
	<sg_stem> == "<stem>" Gradation_Res:<nt> e
	<sg part> == "<stem Gradation:<"<gt>" part sg>>" tt Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<pl_stem> == "<stem>" nsi
	<pl ill> == NOMINAL:<"<gt>" pl ill in>
	<pl gen> == NOMINAL:<"<gt>" pl gen en>
	<pl part> == NOMINAL:<"<gt>" pl part a>.

% Active past participles. Ending in ⟨-ee-⟩ (⟨-ee⟩ > ⟨-ut⟩or⟨-yt⟩ in the nominative singular), 2+ syllables. No gradation.
% kuollut
% 1013
Type47:
	<gt> == none
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>" Harmony:<"<harmony>" u> t
	<sg_stem> == "<stem>" ee
	<sg part> == <sg nom> t Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill seen>
	
	<pl_stem> == "<stem>" ei
	<pl ill> == NOMINAL:<"<gt>" pl ill hin> xxx NOMINAL:<"<gt>" pl ill siin>
	<pl gen> == NOMINAL:<"<gt>" pl gen den>
	<pl part> == NOMINAL:<"<gt>" pl part ta>.

% Ending in ⟨-ee-⟩ (⟨-ee⟩ > ⟨-e⟩ˣ in the nominative singular), 2+ syllables. With penultimate-syllable gradation.➡️
% hame
% 7791
Type48:
	<gt> == rev
	<> == NOMINAL:<"<gt>">
	<sg nom> == "<stem Gradation:<"<gt>" nom sg>>"
	<sg_stem> == "<stem>" e
	<sg part> == "<stem Gradation:<"<gt>" part sg>>" tt Harmony:<"<harmony>" a>
	<sg ill> == NOMINAL:<"<gt>" sg ill seen>
	
	<pl_stem> == "<stem>" i
	<pl ill> == NOMINAL:<"<gt>" pl ill hin> xxx NOMINAL:<"<gt>" pl ill siin>
	<pl gen> == NOMINAL:<"<gt>" pl gen den>
	<pl part> == NOMINAL:<"<gt>" pl part ta>.

% As either hame xxx sisar. Covered by inheriting from either Type48 xxx Type32 in the leaf
% askel xxx askele
% 188
% Type49:

% A pseudo-type for compounds in which only the final part is inflected. Doesn't need its own class here, first part of compound can be treated as static prefix to second part
% isoäiti
% -
% Type50:

% A pseudo-type for compounds in which all parts are inflected. Not possible with current approach because both parts could have different harmony
% nuoripari
% 1091
% Type51:

% Word leaf nodes

Valo:
<> == Type1
<stem> == valo
<final vowel> == o
<harmony> == back.

Katu:
<> == Type1
<stem> == ka Gradation_Res:<t> u
<final vowel> == u
<harmony> == back.

Palvelu:
<> == Type2
<stem> == palvelu
<final vowel> == u
<harmony> == back.

Valtio:
<> == Type3
<stem> == valtio
<final vowel> == o
<harmony> == back.

Laatikko:
<> == Type4
<stem> == laati Gradation_Res:<kk> o
<harmony> == back.

Risti:
<> == Type5
<stem> == rist
<harmony> == front.

Koti:
<> == Type5
<stem> == ko Gradation_Res:<t>
<harmony> == back.

Paperi:
<> == Type6
<stem> == paper
<harmony> == back.

Ovi:
<> == Type7
<stem> == ov
<harmony> == back.

Lahti:
<> == Type7
<stem> == lah Gradation_Res:<t>
<harmony> == back.

Nalle:
<> == Type8
<stem> == nalle
<harmony> == back.

Nukke:
<> == Type8
<stem> == nu Gradation_Res:<kk> e
<harmony> == back.

Kala:
<> == Type9
<stem> == kal
<harmony> == back.

Rauta:
<> == Type9
<stem> == rau Gradation_Res:<t>
<harmony> == back.

Koira:
<> == Type10
<stem> == koir
<harmony> == back.

Sota:
<> == Type10
<stem> == so Gradation_Res:<t>
<harmony> == back.

Omena:
<> == Type11
<stem> == omen
<harmony> == back.

Kulkija:
<> == Type12
<stem> == kulkij
<harmony> == back.

Katiska:
<> == Type13
<stem> == katisk
<harmony> == back.

Solakka:
<> == Type14
<stem> == sola Gradation_Res:<kk>
<harmony> == back.

Navetta:
<> == Type14
<stem> == nave Gradation_Res:<tt>
<harmony> == back.

Korkea:
<> == Type15
<stem> == korke
<harmony> == back.

Vanhempi:
<> == Type16
<stem> == vanhe Gradation_Res:<mp>
<harmony> == back.

Isompi:
<> == Type16
<stem> == iso Gradation_Res:<mp>
<harmony> == back.

Vapaa:
<> == Type17
<stem> == vapa
<final vowel> == a
<harmony> == back.

Maa:
<> == Type18
<stem> == ma
<final vowel> == Harmony:<"<harmony>" a>
<harmony> == back.

Voi:
<> == Type18
<stem> == vo
<final vowel> == i
<harmony> == back.

Suo:
<> == Type19uo
<stem> == s
<final vowel> == o
<harmony> == back.

Tie:
<> == Type19ie
<stem> == t
<final vowel> == e
<harmony> == front.

Työ:
<> == Type19uo
<stem> == t
<final vowel> == ö
<harmony> == front.

Filee:
<> == Type20
<stem> == file
<final vowel> == e
<harmony> == front.

Rosé:
<> == Type21
<stem> == rosé
<final vowel> == e
<harmony> == back.

Parfait:
<> == Type22
<stem> == parfait
<harmony> == back.

Tiili:
<> == Type23
<stem> == tiil
<harmony> == front.

Uni:
<> == Type24
<stem> == un
<harmony> == back.

Toimi:
<> == Type25
<stem> == toi
<harmony> == back.

Pieni:
<> == Type26
<stem> == pien
<harmony> == front.

Käsi:
<> == Type27
<stem> == kä
<harmony> == front.

Uusi:
<> == Type27
<stem> == uu
<harmony> == back.

Kynsi:
<> == Type28nt
<stem> == ky
<harmony> == front.

Korsi:
<> == Type28rt
<stem> == ko
<harmony> == back.

Jälsi:
<> == Type28lt
<stem> == jä
<harmony> == front.

Lapsi:
<> == Type29
<stem> == la
<harmony> == back.

Veitsi:
<> == Type30
<stem> == vei
<harmony> == front.

Kaksi:
<> == Type31
<stem> == ka
<harmony> == back.

Yksi:
<> == Type31
<stem> == y
<harmony> == front.

Sisar:
<> == Type32
<stem> == sisar
<harmony> == back.

Tatar:
<> == Type32
<stem> == ta Gradation_Res:<tt> ar
<harmony> == back.

Kytkin:
<> == Type33
<stem> == kytki
<harmony> == front.

Laidun:
<> == Type33
<stem> == lai Gradation_Res:<t> u
<harmony> == back.

Onneton:
<> == Type34
<stem> == onne Gradation_Res:<tt> o
<harmony> == back.

Nimetön:
<> == Type34
<stem> == nime Gradation_Res:<tt> ö
<harmony> == front.

Lämmin:
<> == Type35
<stem> == lä Gradation_Res:<mp> i
<harmony> == front.

Hapan:
<> == Type35
<stem> == ha Gradation_Res:<pp> a
<harmony> == back.

Sisin:
<> == Type36
<stem> == sisi
<harmony> == front.

Pahin:
<> == Type36
<stem> == pahi
<harmony> == back.

Vasen:
<> == Type37
<stem> == vase
<harmony> == back.

Nainen:
<> == Type38
<stem> == nai
<harmony> == back.

Vastaus:
<> == Type39
<stem> == vastau
<harmony> == back.

Kalleus:
<> == Type40
<stem> == kalleu
<harmony> == back.

Kauneus:
<> == Type40
<stem> == kauneu
<harmony> == back.

Vieras:
<> == Type41
<stem> == viera
<final vowel> == a
<harmony> == back.

Keidas:
<> == Type41
<stem> == kei Gradation_Res:<t> a
<final vowel> == a
<harmony> == back.

Mies:
<> == Type42
<stem> == mie
<harmony> == front.

Ohut:
<> == Type43
<stem> == ohu
<harmony> == back.

Immyt:
<> == Type43
<stem> == i Gradation_Res:<mp> y
<harmony> == front.

Kevät:
<> == Type44
<stem> == kevä
<harmony> == front.

Kahdeksas:
<> == Type45
<stem> == kahdeksa
<harmony> == back.

Kolmas:
<> == Type45
<stem> == kolma
<harmony> == back.

Tuhat:
<> == Type46
<stem> == tuha
<harmony> == back.

Kuollut:
<> == Type47
<stem> == kuoll
<harmony> == back.

Hame:
<> == Type48
<stem> == hame
<harmony> == back.

Sade:
<> == Type48
<stem> == sa Gradation_Res:<t> e
<harmony> == back.

%% Currently not covered

Askel:
<> == Type49
<stem> == askel
<harmony> == back.

Isoäiti:
<> == Type50
<stem> == isoäiti
<harmony> == front.

Nuoripari:
<> == Type51
<stem> == nuoripari
<harmony> == back.