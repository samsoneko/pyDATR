#vars $grad_type : for rev none.
#vars $number : sg pl.

% Base node for all nominals, used for completing case endings
NOMINAL:
	% Only plural queries end up here
	<$grad_type pl nom> == "<mor sg stem Gradation:<phon $grad_type nom pl>>" t

	% Sentences for the partitive
	<$grad_type sg part a> == "<mor sg stem Gradation:<phon $grad_type part sg a>>" Harmony:<phon "<phon harmony>" a>
	<$grad_type sg part ta> == "<mor sg stem Gradation:<phon $grad_type part sg ta>>" t Harmony:<phon "<phon harmony>" a>
	<$grad_type cs part ta> == "<mor cons stem Gradation:<phon $grad_type part sg ta>>" t Harmony:<phon "<phon harmony>" a>
	
	<$grad_type pl part a> == "<mor pl stem Gradation:<phon $grad_type part pl a>>" Harmony:<phon "<phon harmony>" a>
	<$grad_type pl part ta> == "<mor pl stem Gradation:<phon $grad_type part pl ta>>" t Harmony:<phon "<phon harmony>" a>
	
	% Sentences for the genitive
	<$grad_type sg gen> == "<mor sg stem Gradation:<phon $grad_type gen sg>>" n
	
	<$grad_type pl gen en> == "<mor pl stem Gradation:<phon $grad_type gen pl en>>" en
	<$grad_type pl gen den> == "<mor pl stem Gradation:<phon $grad_type gen pl den>>" den
	<$grad_type cs gen ten> == "<mor cons stem Gradation:<phon $grad_type gen pl ten>>" ten

	% Sentences for the partitive
	<$grad_type sg ill vn> == "<mor sg stem Gradation:<phon $grad_type ill sg vn>>" "<final vowel>" n
	<$grad_type sg ill hvn> == "<mor sg stem Gradation:<phon $grad_type ill sg hvn>>" h "<final vowel>" n
	<$grad_type sg ill seen> == "<mor sg stem Gradation:<phon $grad_type ill sg seen>>" seen
	
	<$grad_type pl ill in> == "<mor pl stem Gradation:<phon $grad_type ill pl in>>" in
	<$grad_type pl ill hin> == "<mor pl stem Gradation:<phon $grad_type ill pl hin>>" hin
	<$grad_type pl ill siin> == "<mor pl stem Gradation:<phon $grad_type ill pl siin>>" siin
	
	% Essive always in stong form
	<$grad_type $number ess> == "<mor $number stem Gradation:<phon $grad_type ess $number>>" n Harmony:<phon "<phon harmony>" a>
	
	% "Regular" cases
	<$grad_type $number iness> == "<mor $number stem Gradation:<phon $grad_type iness $number>>" ss Harmony:<phon "<phon harmony>" a>
	<$grad_type $number ela> == "<mor $number stem Gradation:<phon $grad_type ela $number>>" st Harmony:<phon "<phon harmony>" a>
	<$grad_type $number all> == "<mor $number stem Gradation:<phon $grad_type all $number>>" lle
	<$grad_type $number adess> == "<mor $number stem Gradation:<phon $grad_type adess $number>>" ll Harmony:<phon "<phon harmony>" a>
	<$grad_type $number abl> == "<mor $number stem Gradation:<phon $grad_type abl $number>>" lt Harmony:<phon "<phon harmony>" a>
	
	<$grad_type $number trans> == "<mor $number stem Gradation:<phon $grad_type trans $number>>" ksi
	<$grad_type $number abess> == "<mor $number stem Gradation:<phon $grad_type abess $number>>" tt Harmony:<phon "<phon harmony>" a>
	<$grad_type pl instr> == "<mor pl stem Gradation:<phon $grad_type instr pl>>" n.

Harmony:
	<phon front a> == ä
	<phon front o> == ö
	<phon front u> == y
	<phon back a> == a
	<phon back o> == o
	<phon back u> == u.

Gradation_Res:
	<phon pp strong> == pp
	<phon pp weak> == p
	<phon tt strong> == tt
	<phon tt weak> == t
	<phon kk strong> == kk
	<phon kk weak> == k
	
	<phon mp strong> == mp
	<phon mp weak> == mm
	<phon nt strong> == nt
	<phon nt weak> == nn
	<phon lt strong> == lt
	<phon lt weak> == ll
	<phon rt strong> == rt
	<phon rt weak> == rr
	<phon nk strong> == nk
	<phon nk weak> == ng
	
	<phon p strong> == p
	<phon p weak> == v
	<phon t strong> == t
	<phon t weak> == d
	<phon k strong> == k
	<phon k weak> == .

Gradation:
	<phon none> == none

	<phon for> == weak
	<phon for nom sg> == strong
	<phon for part> == strong
	<phon for gen pl en> == strong
	<phon for part pl ta> == weak
	<phon for ill> == strong
	<phon for ess> == strong

	<phon rev> == strong
	<phon rev nom sg> == weak
	<phon rev gen pl ten> == weak
	<phon rev part sg> == weak.

% Declension types based on Kotimaisten kielten keskus

% Ending in ⟨-o⟩, ⟨-u⟩, ⟨-ö⟩, ⟨-y⟩, 2 syllables, and past passive participles with any number of syllables, With final-syllable gradation.
% valo
% 18340
Type1:
	<gt> == for
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>"
	<mor sg stem> == "<mor root>"
	<mor sg part> == NOMINAL:<"<gt>" sg part a>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-o⟩, ⟨-u⟩, ⟨-ö⟩, ⟨-y⟩, 3+ syllables, No gradation.
% palvelu
% 4610
Type2:
	<gt> == none
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>"
	<mor sg stem> == "<mor root>"
	<mor sg part> == NOMINAL:<"<gt>" sg part a>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en> _ NOMINAL:<"<gt>" pl gen den>
	<mor pl part> == NOMINAL:<"<gt>" pl part a> _ NOMINAL:<"<gt>" pl part ta>.

% Ending in ⟨-ao⟩, ⟨-eo⟩, ⟨-io⟩, ⟨-iö⟩, ⟨-ie⟩, ⟨-oe⟩, No gradation.
% valtio
% 2640
Type3:
	<gt> == none
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>"
	<mor sg stem> == "<mor root>"
	<mor sg part> == NOMINAL:<"<gt>" sg part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen den>
	<mor pl part> == NOMINAL:<"<gt>" pl part ta>.

% Ending in ⟨-kko⟩, ⟨-kkö⟩, ⟨-tto⟩, 3+ syllables, With final-syllable gradation.
% laatikko
% 1094
Type4:
	<gt> == for
	<final vowel> == o
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>"
	<mor sg stem> == "<mor root>"
	<mor sg part> == NOMINAL:<"<gt>" sg part a>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en> _ NOMINAL:<"<gt>" pl gen den>
	<mor pl part> == NOMINAL:<"<gt>" pl part a> _ NOMINAL:<"<gt>" pl part ta>.

% Ending in ⟨-i⟩ (possibly no vowel in the nominative singular), 2 syllables, With final-syllable gradation.
% risti
% 22258
Type5:
	<gt> == for
	<final vowel> == i
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" i
	<mor sg stem> == "<mor root>" i
	<mor sg part> == NOMINAL:<"<gt>" sg part a>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" ei
	<mor pl ill> == NOMINAL:<"<gt>" pl ill hin>
	% Extra rule for manual genitive based on nominative
	<mor pl gen> == <mor sg nom> en
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-i⟩ (possibly no vowel in the nominative singular), 3+ syllables, No gradation.
% paperi
% 5752
Type6:
	<gt> == none
	<final vowel> == i
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" i
	<mor sg stem> == "<mor root>" i
	<mor sg part> == NOMINAL:<"<gt>" sg part a>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" ei
	<mor pl ill> == NOMINAL:<"<gt>" pl ill hin>
	% Extra rule for manual genitive based on nominative
	<mor pl gen> == <mor sg nom> en _ NOMINAL:<"<gt>" pl gen den>
	<mor pl part> == NOMINAL:<"<gt>" pl part a> _ NOMINAL:<"<gt>" pl part ta>.

% Ending in ⟨-e⟩ (⟨-e⟩ > ⟨-i⟩ in the nominative singular), 2 syllables, With final-syllable gradation.
% ovi
% 2515
Type7:
	<gt> == for
	<final vowel> == e
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" i
	<mor sg stem> == "<mor root>" e
	<mor sg part> == NOMINAL:<"<gt>" sg part a>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill in>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-e⟩, 2 syllables, With final-syllable gradation.
% nalle
% 391
Type8:
	<gt> == for
	<final vowel> == e
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>"
	<mor sg stem> == "<mor root>"
	<mor sg part> == NOMINAL:<"<gt>" sg part a>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-a⟩, 2 syllables, Non-rounded vowel (a, e, i) in first syllable, With final-syllable gradation.
% kala
% 11733
Type9:
	<gt> == for
	<final vowel> == Harmony:<phon "<phon harmony>" a>
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" Harmony:<phon "<phon harmony>" a>
	<mor sg stem> == "<mor root>" Harmony:<phon "<phon harmony>" a>
	<mor sg part> == NOMINAL:<"<gt>" sg part a>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" Harmony:<phon "<phon harmony>" o> i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-a⟩ or ⟨-ä⟩, 2 syllables, If ending in ⟨-a⟩, rounded vowel (⟨o⟩ or ⟨u⟩) in first syllable, With final-syllable gradation.
% koira
% 15597
Type10:
	<gt> == for
	<final vowel> == Harmony:<phon "<phon harmony>" a>
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" Harmony:<phon "<phon harmony>" a>
	<mor sg stem> == "<mor root>" Harmony:<phon "<phon harmony>" a>
	<mor sg part> == NOMINAL:<"<gt>" sg part a>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill in>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in short consonant + ⟨-a⟩ or ⟨-ä⟩, 3+ syllables. No gradation.
% omena
% 165
Type11:
	<gt> == none
	<final vowel> == Harmony:<phon "<phon harmony>" a>
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" Harmony:<phon "<phon harmony>" a>
	<mor sg stem> == "<mor root>" Harmony:<phon "<phon harmony>" a>
	<mor sg part> == NOMINAL:<"<gt>" sg part a>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" Harmony:<phon "<phon harmony>" o> i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen den>
	<mor pl part> == NOMINAL:<"<gt>" pl part ta>.

% Ending in ⟨-a⟩ or ⟨-ä⟩, 3+ syllables. No gradation.
% kulkija
% 6726
Type12:
	<gt> == none
	<final vowel> == Harmony:<phon "<phon harmony>" a>
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" Harmony:<phon "<phon harmony>" a>
	<mor sg stem> == "<mor root>" Harmony:<phon "<phon harmony>" a>
	<mor sg part> == NOMINAL:<"<gt>" sg part a>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" Harmony:<phon "<phon harmony>" o> i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen den>
	<mor pl part> == NOMINAL:<"<gt>" pl part ta>.

% Ending in ⟨-a⟩ or ⟨-ä⟩, 3+ syllables. No gradation.
% katsika
% 519
Type13:
	<gt> == none
	<final vowel> == Harmony:<phon "<phon harmony>" a>
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" Harmony:<phon "<phon harmony>" a>
	<mor sg stem> == "<mor root>" Harmony:<phon "<phon harmony>" a>
	<mor sg part> == NOMINAL:<"<gt>" sg part a>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" Harmony:<phon "<phon harmony>" o> i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en> _ NOMINAL:<"<gt>" pl gen den>
	<mor pl part> == NOMINAL:<"<gt>" pl part a> _ NOMINAL:<"<gt>" pl part ta>.

% Ending in long consonant + ⟨-a⟩ or ⟨-ä⟩, 3+ syllables. With final-syllable gradation.
% solakka
% 1081
Type14:
	<gt> == for
	<final vowel> == Harmony:<phon "<phon harmony>" a>
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" Harmony:<phon "<phon harmony>" a>
	<mor sg stem> == "<mor root>" Harmony:<phon "<phon harmony>" a>
	<mor sg part> == NOMINAL:<"<gt>" sg part a>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" Harmony:<phon "<phon harmony>" o> i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en> _ NOMINAL:<"<gt>" pl gen den>
	<mor pl part> == NOMINAL:<"<gt>" pl part a> _ NOMINAL:<"<gt>" pl part ta>.

% Ending in ⟨-ea⟩, ⟨-eä⟩, ⟨-oa⟩. No gradation.
% korkea
% 410
Type15:
	<gt> == none
	<final vowel> == Harmony:<phon "<phon harmony>" a>
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" Harmony:<phon "<phon harmony>" a>
	<mor sg stem> == "<mor root>" Harmony:<phon "<phon harmony>" a>
	<mor sg part> == NOMINAL:<"<gt>" sg part a> _ NOMINAL:<"<gt>" sg part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill hin> _ NOMINAL:<"<gt>" pl ill siin>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen den>
	<mor pl part> == NOMINAL:<"<gt>" pl part ta>.

% Comparative adjectives. Ending in ⟨-mpa⟩, ⟨-mpä⟩ (⟨-a⟩or⟨-ä⟩ > ⟨-i⟩ in the nominative singular). With final-syllable gradation.
% vanhempi
% 500
Type16:
	<gt> == for
	<final vowel> == Harmony:<phon "<phon harmony>" a>
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" i
	<mor sg stem> == "<mor root>" Harmony:<phon "<phon harmony>" a>
	<mor sg part> == NOMINAL:<"<gt>" sg part a>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill in>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-aa⟩, ⟨-oo⟩, ⟨-uu⟩, rarely ⟨-ee⟩, ⟨-yy⟩, 2+ syllables. No gradation.
% vapaa
% 276
Type17:
	<gt> == none
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" "<final vowel>"
	<mor sg stem> == "<mor root>" "<final vowel>"
	<mor sg part> == NOMINAL:<"<gt>" sg part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill seen>
	
	<mor pl stem> == "<mor root>" i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill siin>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen den>
	<mor pl part> == NOMINAL:<"<gt>" pl part ta>.

% Ending in a long vowel _ a diphthong ending in ⟨-i⟩, 1 syllable. No gradation.
% maa
% 1464
Type18:
	<gt> == none
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" "<final vowel>"
	<mor sg stem> == "<mor root>" "<final vowel>"
	<mor sg part> == NOMINAL:<"<gt>" sg part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill hvn>
	
	<mor pl stem> == "<mor root>" i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen den>
	<mor pl part> == NOMINAL:<"<gt>" pl part ta>.

% Ending in ⟨-ie⟩, ⟨-uo⟩, ⟨-yö⟩, 1 syllable. No gradation.
% suo
% 662
Type19ie:
	<gt> == none
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" ie
	<mor sg stem> == "<mor root>" ie
	<mor sg part> == NOMINAL:<"<gt>" sg part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill hvn>
	
	<mor pl stem> == "<mor root>" ei
	<mor pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen den>
	<mor pl part> == NOMINAL:<"<gt>" pl part ta>.

Type19uo:
	<gt> == none
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" Harmony:<phon "<phon harmony>" u> Harmony:<phon "<phon harmony>" o>
	<mor sg stem> == "<mor root>" Harmony:<phon "<phon harmony>" u> Harmony:<phon "<phon harmony>" o>
	<mor sg part> == NOMINAL:<"<gt>" sg part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill hvn>
	
	<mor pl stem> == "<mor root>" Harmony:<phon "<phon harmony>" o> i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen den>
	<mor pl part> == NOMINAL:<"<gt>" pl part ta>.

% Ending in ⟨-ee⟩, ⟨-öö⟩, ⟨-yy⟩, rarely ⟨-aa⟩, ⟨-oo⟩, ⟨-uu⟩, 2+ syllables. No gradation.
% filee
% 145
Type20:
	<gt> == none
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" "<final vowel>"
	<mor sg stem> == "<mor root>" "<final vowel>"
	<mor sg part> == NOMINAL:<"<gt>" sg part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill hvn> _ NOMINAL:<"<gt>" sg ill seen>
	
	<mor pl stem> == "<mor root>" i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill hin> _ NOMINAL:<"<gt>" pl ill siin>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen den>
	<mor pl part> == NOMINAL:<"<gt>" pl part ta>.

% Foreign words ending in a vowel. No gradation.
% rosé
% 59
Type21:
	<gt> == none
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>"
	<mor sg stem> == "<mor root>"
	<mor sg part> == NOMINAL:<"<gt>" sg part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill hvn>
	
	<mor pl stem> == "<mor root>" i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen den>
	<mor pl part> == NOMINAL:<"<gt>" pl part ta>.

% Foreign words ending in a vowel, but written with a final consonant (mostly French loanwords). No gradation.
% parfait
% 22
Type22:
	<gt> == none
	<final vowel> == e
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>"
	<mor sg stem> == "<mor root>" ’
	<mor sg part> == NOMINAL:<"<gt>" sg part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill hvn>
	
	<mor pl stem> == "<mor root>" ’i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill hin>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen den>
	<mor pl part> == NOMINAL:<"<gt>" pl part ta>.

% Ending in ⟨-he-⟩, ⟨-le-⟩, rarely ⟨-ne-⟩ (⟨-e⟩ > ⟨-i⟩ in the nominative singular), 2 syllables. No gradation.
% tiili
% 60
Type23:
	<gt> == none
	<final vowel> == e
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" i
	<mor sg stem> == "<mor root>" e
	<mor cons stem> == "<mor root>"
	<mor sg part> == NOMINAL:<"<gt>" cs part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill in>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-he-⟩, ⟨-le-⟩, ⟨-ne-⟩, ⟨-re-⟩, ⟨-se-⟩ (⟨-e⟩ > ⟨-i⟩ in the nominative singular), 2 syllables. No gradation.
% uni
% 412
Type24:
	<gt> == none
	<final vowel> == e
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" i
	<mor sg stem> == "<mor root>" e
	<mor cons stem> == "<mor root>"
	<mor sg part> == NOMINAL:<"<gt>" cs part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill in>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en> _ NOMINAL:<"<gt>" cs gen ten>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-me-⟩ (⟨-e⟩ > ⟨-i⟩ in the nominative singular), 2 syllables. No gradation.
% toimi
% 299
Type25:
	<gt> == none
	<final vowel> == e
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" mi
	<mor sg stem> == "<mor root>" me
	<mor cons stem> == "<mor root>" n
	<mor sg part> == NOMINAL:<"<gt>" cs part ta> _ NOMINAL:<"<gt>" sg part a>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" mi
	<mor pl ill> == NOMINAL:<"<gt>" pl ill in>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en> _ NOMINAL:<"<gt>" cs gen ten>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-le-⟩, ⟨-ne-⟩, ⟨-re-⟩ (⟨-e⟩ > ⟨-i⟩ in the nominative singular), 2 syllables. No gradation.
% pieni
% 1008
Type26:
	<gt> == none
	<final vowel> == e
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" i
	<mor sg stem> == "<mor root>" e
	<mor cons stem> == "<mor root>"
	<mor sg part> == NOMINAL:<"<gt>" cs part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill in>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en> _ NOMINAL:<"<gt>" cs gen ten>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-te-⟩ (⟨-te⟩ > ⟨-si⟩ in the nominative singular), 2 syllables. With final-syllable gradation.
% käsi
% 612
Type27:
	<gt> == for
	<final vowel> == e
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" si
	<mor sg stem> == "<mor root>" Gradation_Res:<phon t> e
	<mor cons stem> == "<mor root>" t
	<mor sg part> == NOMINAL:<"<gt>" cs part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" si
	<mor pl ill> == NOMINAL:<"<gt>" pl ill in>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en> _ NOMINAL:<"<gt>" cs gen ten>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-lte-⟩, ⟨-nte-⟩, ⟨-rte-⟩ (⟨-te⟩ > ⟨-si⟩ in the nominative singular), 2 syllables. With final-syllable gradation.
% kynsi
% 208
Type28nt:
	<gt> == for
	<final vowel> == e
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" nsi
	<mor sg stem> == "<mor root>" Gradation_Res:<phon nt> e
	<mor cons stem> == "<mor root>" nt
	<mor sg part> == NOMINAL:<"<gt>" cs part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" nsi
	<mor pl ill> == NOMINAL:<"<gt>" pl ill in>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en> _ NOMINAL:<"<gt>" cs gen ten>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

Type28rt:
	<gt> == for
	<final vowel> == e
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" rsi
	<mor sg stem> == "<mor root>" Gradation_Res:<phon rt> e
	<mor cons stem> == "<mor root>" rt
	<mor sg part> == NOMINAL:<"<gt>" cs part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" rsi
	<mor pl ill> == NOMINAL:<"<gt>" pl ill in>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en> _ NOMINAL:<"<gt>" cs gen ten>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

Type28lt:
	<gt> == for
	<final vowel> == e
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" lsi
	<mor sg stem> == "<mor root>" Gradation_Res:<phon lt> e
	<mor cons stem> == "<mor root>" lt
	<mor sg part> == NOMINAL:<"<gt>" cs part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" lsi
	<mor pl ill> == NOMINAL:<"<gt>" pl ill in>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en> _ NOMINAL:<"<gt>" cs gen ten>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-pse-⟩, ⟨-kse-⟩ (⟨-e⟩ > ⟨-i⟩ in the nominative singular), 2 syllables. No gradation.
% lapsi
% 70
Type29:
	<gt> == none
	<final vowel> == e
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" psi
	<mor sg stem> == "<mor root>" pse
	<mor cons stem> == "<mor root>" s
	<mor sg part> == NOMINAL:<"<gt>" cs part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" psi
	<mor pl ill> == NOMINAL:<"<gt>" pl ill in>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en> _ NOMINAL:<"<gt>" cs gen ten>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-tse-⟩ (⟨-e⟩ > ⟨-i⟩ in the nominative singular), 2 syllables. No gradation.
% veitsi
% 55
Type30:
	<gt> == none
	<final vowel> == e
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" tsi
	<mor sg stem> == "<mor root>" tse
	<mor cons stem> == "<mor root>" s
	<mor sg part> == NOMINAL:<"<gt>" cs part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" tsi
	<mor pl ill> == NOMINAL:<"<gt>" pl ill in>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en> _ NOMINAL:<"<gt>" cs gen ten>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-hte-⟩ (⟨-hte⟩ > ⟨-ksi⟩ in the nominative singular), 2 syllables. With final-syllable gradation.
% kaksi
% 16
Type31:
	<gt> == for
	<final vowel> == e
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" ksi
	<mor sg stem> == "<mor root>" h Gradation_Res:<phon t> e
	<mor cons stem> == "<mor root>" h
	<mor sg part> == NOMINAL:<"<gt>" cs part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" ksi
	<mor pl ill> == NOMINAL:<"<gt>" pl ill in>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-le-⟩, ⟨-ne-⟩, ⟨-re-⟩ (no vowel in the nominative singular), 3+ syllables. With penultimate-syllable gradation.➡️
% sisar
% 373
Type32:
	<gt> == rev
	<final vowel> == e
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>"
	<mor sg stem> == "<mor root>" e
	<mor cons stem> == "<mor root>"
	<mor sg part> == NOMINAL:<"<gt>" cs part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill in>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en> _ NOMINAL:<"<gt>" cs gen ten>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-me-⟩ (⟨-me⟩ > ⟨-n⟩ in the nominative singular), 3+ syllables. With penultimate-syllable gradation.➡️
% kytkin
% 1884
Type33:
	<gt> == rev
	<final vowel> == e
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" n
	<mor sg stem> == "<mor root>" me
	<mor cons stem> == "<mor root>" n
	<mor sg part> == NOMINAL:<"<gt>" cs part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" mi
	<mor pl ill> == NOMINAL:<"<gt>" pl ill in>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en> _ NOMINAL:<"<gt>" cs gen ten>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-toma-⟩, ⟨-tömä-⟩ (⟨-ma⟩or⟨-mä⟩ > ⟨-n⟩ in the nominative singular), 3+ syllables. With penultimate-syllable gradation.➡️
% onneton
% 1124
Type34:
	<gt> == rev
	<final vowel> == Harmony:<phon "<phon harmony>" a>
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" n
	<mor sg stem> == "<mor root>" m Harmony:<phon "<phon harmony>" a>
	<mor cons stem> == "<mor root>" n
	<mor sg part> == NOMINAL:<"<gt>" cs part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" mi
	<mor pl ill> == NOMINAL:<"<gt>" pl ill in>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en> _ NOMINAL:<"<gt>" cs gen ten>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

% The words lämmin and hapan only. Ending in ⟨-mä-⟩ (⟨-mä⟩ > ⟨-n⟩ in the nominative singular), 3 syllables. With penultimate-syllable gradation.➡️
% lämmin, hapan
% 12
Type35:
	<gt> == rev
	<final vowel> == Harmony:<phon "<phon harmony>" a>
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" n
	<mor sg stem> == "<mor root>" m Harmony:<phon "<phon harmony>" a>
	<mor cons stem> == "<mor root>" n
	<mor sg part> == NOMINAL:<"<gt>" cs part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" mi
	<mor pl ill> == NOMINAL:<"<gt>" pl ill in>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

% Superlative adjectives. Ending in ⟨-impa-⟩, ⟨-impä-⟩ (⟨-mpa⟩or⟨-mpä⟩ > ⟨-n⟩ in the nominative singular), 2+ syllables. With penultimate-syllable gradation.➡️
% sisin
% 453
Type36:
	<gt> == for
	<final vowel> == Harmony:<phon "<phon harmony>" a>
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" n
	<mor sg stem> == "<mor root>" Gradation_Res:<phon mp> Harmony:<phon "<phon harmony>" a>
	<mor cons stem> == "<mor root>" n
	<mor sg part> == NOMINAL:<"<gt>" cs part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" Gradation_Res:<phon mp> i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill in>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en> _ NOMINAL:<"<gt>" cs gen ten>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

% The word vasen only. Ending in ⟨-mpa-⟩ (⟨-mpa⟩ > ⟨-n⟩ in the nominative singular), 3 syllables. With penultimate-syllable gradation.➡️
% vasen
% 1
Type37:
	<gt> == for
	<final vowel> == Harmony:<phon "<phon harmony>" a>
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" n
	<mor sg stem> == "<mor root>" Gradation_Res:<phon mp> Harmony:<phon "<phon harmony>" a>
	<mor cons stem> == "<mor root>" n
	<mor sg part> == NOMINAL:<"<gt>" cs part ta> _ NOMINAL:<"<gt>" sg part a>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" Gradation_Res:<phon mp> i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill in>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en> _ NOMINAL:<"<gt>" cs gen ten>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-se-⟩ (⟨-se⟩ > ⟨-nen⟩ in the nominative singular), 2+ syllables. No gradation.
% nainen
% 19627
Type38:
	<gt> == none
	<final vowel> == e
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" nen
	<mor sg stem> == "<mor root>" se
	<mor cons stem> == "<mor root>" s
	<mor sg part> == NOMINAL:<"<gt>" cs part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" si
	<mor pl ill> == NOMINAL:<"<gt>" pl ill in>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en> _ NOMINAL:<"<gt>" cs gen ten>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-kse-⟩ (⟨-kse⟩ > ⟨-s⟩ in the nominative singular), 3+ syllables. No gradation.
% vastaus
% 10739
Type39:
	<gt> == none
	<final vowel> == e
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" s
	<mor sg stem> == "<mor root>" kse
	<mor cons stem> == "<mor root>" s
	<mor sg part> == NOMINAL:<"<gt>" cs part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" ksi
	<mor pl ill> == NOMINAL:<"<gt>" pl ill in>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en> _ NOMINAL:<"<gt>" cs gen ten>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-te-⟩ (⟨-te⟩ > ⟨-s⟩ in the nominative singular), 3+ syllables. With final-syllable gradation.
% kalleus
% 6219
Type40:
	<gt> == for
	<final vowel> == e
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" s
	<mor sg stem> == "<mor root>" Gradation_Res:<phon t> e
	<mor cons stem> == "<mor root>" t
	<mor sg part> == NOMINAL:<"<gt>" cs part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" ksi
	<mor pl ill> == NOMINAL:<"<gt>" pl ill in>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.
	
% Ending in long vowel (long vowel > short vowel + ⟨-s⟩ in the nominative singular), 2+ syllables. With penultimate-syllable gradation.➡️
% vieras
% 2104
Type41:
	<gt> == rev
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" s
	<mor sg stem> == "<mor root>" "<final vowel>"
	<mor cons stem> == "<mor root>" s
	<mor sg part> == NOMINAL:<"<gt>" cs part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill seen>
	
	<mor pl stem> == "<mor root>" i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill siin>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen den>
	<mor pl part> == NOMINAL:<"<gt>" pl part ta>.

% The word mies only. Ending in ⟨-he-⟩ (⟨-he⟩ > ⟨-s⟩ in the nominative singular), 2 syllables. No gradation.
% mies
% 393
Type42:
	<gt> == none
	<final vowel> == e
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" s
	<mor sg stem> == "<mor root>" he
	<mor cons stem> == "<mor root>" s
	<mor sg part> == NOMINAL:<"<gt>" cs part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" hi
	<mor pl ill> == NOMINAL:<"<gt>" pl ill in>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en> _ NOMINAL:<"<gt>" cs gen ten>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

% Ending in ⟨-ue-⟩, ⟨-ye-⟩ (⟨-e⟩ > ⟨-t⟩ in the nominative singular), 3+ syllables. With penultimate-syllable gradation.➡️
% ohut
% 66
Type43:
	<gt> == rev
	<final vowel> == e
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" t
	<mor sg stem> == "<mor root>" e
	<mor cons stem> == "<mor root>" t
	<mor sg part> == NOMINAL:<"<gt>" cs part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill hin> _ NOMINAL:<"<gt>" pl ill siin>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen den>
	<mor pl part> == NOMINAL:<"<gt>" pl part ta>.

% Ending in ⟨-ää-⟩ (⟨-ä⟩ > ⟨-t⟩ in the nominative singular), 2+ syllables. No gradation (outside of proper nouns).
% kevät
% 8
Type44:
	<gt> == none
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" t
	<mor sg stem> == "<mor root>" Harmony:<phon "<phon harmony>" a>
	<mor cons stem> == "<mor root>" t
	<mor sg part> == NOMINAL:<"<gt>" cs part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill seen>
	
	<mor pl stem> == "<mor root>" i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill hin> _ NOMINAL:<"<gt>" pl ill siin>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen den>
	<mor pl part> == NOMINAL:<"<gt>" pl part ta>.

% Ordinal numbers. Ending in ⟨-nte-⟩ (⟨-nte⟩ > ⟨-s⟩ in the nominative singular), 3+ syllables. With final-syllable gradation.
% kahdeksas
% 38
Type45:
	<gt> == for
	<final vowel> == e
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" s
	<mor sg stem> == "<mor root>" Gradation_Res:<phon nt> e
	<mor cons stem> == "<mor root>" t
	<mor sg part> == NOMINAL:<"<gt>" cs part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" nsi
	<mor pl ill> == NOMINAL:<"<gt>" pl ill in>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

% The word tuhat only. Ending in ⟨-nte-⟩ (⟨-nte⟩ > ⟨-t⟩ in the nominative singular), 3 syllables. With final-syllable gradation.
% tuhat
% 2
Type46:
	<gt> == for
	<final vowel> == e
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" t
	<mor sg stem> == "<mor root>" Gradation_Res:<phon nt> e
	<mor cons stem> == "<mor root>" t
	<mor sg part> == NOMINAL:<"<gt>" cs part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill vn>
	
	<mor pl stem> == "<mor root>" nsi
	<mor pl ill> == NOMINAL:<"<gt>" pl ill in>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen en>
	<mor pl part> == NOMINAL:<"<gt>" pl part a>.

% Active past participles. Ending in ⟨-ee-⟩ (⟨-ee⟩ > ⟨-ut⟩or⟨-yt⟩ in the nominative singular), 2+ syllables. No gradation.
% kuollut
% 1013
Type47:
	<gt> == none
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>" Harmony:<phon "<phon harmony>" u> t
	<mor sg stem> == "<mor root>" ee
	<mor cons stem> == "<mor root>" Harmony:<phon "<phon harmony>" u> t
	<mor sg part> == NOMINAL:<"<gt>" cs part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill seen>
	
	<mor pl stem> == "<mor root>" ei
	<mor pl ill> == NOMINAL:<"<gt>" pl ill hin> _ NOMINAL:<"<gt>" pl ill siin>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen den>
	<mor pl part> == NOMINAL:<"<gt>" pl part ta>.

% Ending in ⟨-ee-⟩ (⟨-ee⟩ > ⟨-e⟩ˣ in the nominative singular), 2+ syllables. With penultimate-syllable gradation.➡️
% hame
% 7791
Type48:
	<gt> == rev
	<mor> == NOMINAL:<"<gt>">
	<mor sg nom> == "<mor root Gradation:<phon "<gt>" nom sg>>"
	<mor sg stem> == "<mor root>" e
	<mor cons stem> == "<mor root>" t
	<mor sg part> == NOMINAL:<"<gt>" cs part ta>
	<mor sg ill> == NOMINAL:<"<gt>" sg ill seen>
	
	<mor pl stem> == "<mor root>" i
	<mor pl ill> == NOMINAL:<"<gt>" pl ill hin> _ NOMINAL:<"<gt>" pl ill siin>
	<mor pl gen> == NOMINAL:<"<gt>" pl gen den>
	<mor pl part> == NOMINAL:<"<gt>" pl part ta>.

% As either hame _ sisar. Covered by inheriting from either Type48 _ Type32 in the leaf
% askel _ askele
% 188
% Type49:

% A pseudo-type for compounds in which only the final part is inflected. Doesn't need its own class here, first part of compound can be treated as static prefix to second part
% isoäiti
% -
% Type50:

% A pseudo-type for compounds in which all parts are inflected. Not possible with current approach because both parts could have different harmony
% nuoripari
% 1091
% Type51:

% Word leaf nodes

Valo:
<> == Type1
<mor root> == valo
<final vowel> == o
<phon harmony> == back.

Katu:
<> == Type1
<mor root> == ka Gradation_Res:<phon t> u
<final vowel> == u
<phon harmony> == back.

Palvelu:
<> == Type2
<mor root> == palvelu
<final vowel> == u
<phon harmony> == back.

Valtio:
<> == Type3
<mor root> == valtio
<final vowel> == o
<phon harmony> == back.

Laatikko:
<> == Type4
<mor root> == laati Gradation_Res:<phon kk> o
<phon harmony> == back.

Risti:
<> == Type5
<mor root> == rist
<phon harmony> == front.

Koti:
<> == Type5
<mor root> == ko Gradation_Res:<phon t>
<phon harmony> == back.

Paperi:
<> == Type6
<mor root> == paper
<phon harmony> == back.

Ovi:
<> == Type7
<mor root> == ov
<phon harmony> == back.

Lahti:
<> == Type7
<mor root> == lah Gradation_Res:<phon t>
<phon harmony> == back.

Nalle:
<> == Type8
<mor root> == nalle
<phon harmony> == back.

Nukke:
<> == Type8
<mor root> == nu Gradation_Res:<phon kk> e
<phon harmony> == back.

Kala:
<> == Type9
<mor root> == kal
<phon harmony> == back.

Rauta:
<> == Type9
<mor root> == rau Gradation_Res:<phon t>
<phon harmony> == back.

Koira:
<> == Type10
<mor root> == koir
<phon harmony> == back.

Sota:
<> == Type10
<mor root> == so Gradation_Res:<phon t>
<phon harmony> == back.

Omena:
<> == Type11
<mor root> == omen
<phon harmony> == back.

Kulkija:
<> == Type12
<mor root> == kulkij
<phon harmony> == back.

Katiska:
<> == Type13
<mor root> == katisk
<phon harmony> == back.

Solakka:
<> == Type14
<mor root> == sola Gradation_Res:<phon kk>
<phon harmony> == back.

Navetta:
<> == Type14
<mor root> == nave Gradation_Res:<phon tt>
<phon harmony> == back.

Korkea:
<> == Type15
<mor root> == korke
<phon harmony> == back.

Vanhempi:
<> == Type16
<mor root> == vanhe Gradation_Res:<phon mp>
<phon harmony> == back.

Isompi:
<> == Type16
<mor root> == iso Gradation_Res:<phon mp>
<phon harmony> == back.

Vapaa:
<> == Type17
<mor root> == vapa
<final vowel> == a
<phon harmony> == back.

Maa:
<> == Type18
<mor root> == ma
<final vowel> == Harmony:<phon "<phon harmony>" a>
<phon harmony> == back.

Voi:
<> == Type18
<mor root> == vo
<final vowel> == i
<phon harmony> == back.

Suo:
<> == Type19uo
<mor root> == s
<final vowel> == o
<phon harmony> == back.

Tie:
<> == Type19ie
<mor root> == t
<final vowel> == e
<phon harmony> == front.

Työ:
<> == Type19uo
<mor root> == t
<final vowel> == ö
<phon harmony> == front.

Filee:
<> == Type20
<mor root> == file
<final vowel> == e
<phon harmony> == front.

Rosé:
<> == Type21
<mor root> == rosé
<final vowel> == e
<phon harmony> == back.

Parfait:
<> == Type22
<mor root> == parfait
<phon harmony> == back.

Tiili:
<> == Type23
<mor root> == tiil
<phon harmony> == front.

Uni:
<> == Type24
<mor root> == un
<phon harmony> == back.

Toimi:
<> == Type25
<mor root> == toi
<phon harmony> == back.

Pieni:
<> == Type26
<mor root> == pien
<phon harmony> == front.

Käsi:
<> == Type27
<mor root> == kä
<phon harmony> == front.

Uusi:
<> == Type27
<mor root> == uu
<phon harmony> == back.

Kynsi:
<> == Type28nt
<mor root> == ky
<phon harmony> == front.

Korsi:
<> == Type28rt
<mor root> == ko
<phon harmony> == back.

Jälsi:
<> == Type28lt
<mor root> == jä
<phon harmony> == front.

Lapsi:
<> == Type29
<mor root> == la
<phon harmony> == back.

Veitsi:
<> == Type30
<mor root> == vei
<phon harmony> == front.

Kaksi:
<> == Type31
<mor root> == ka
<phon harmony> == back.

Yksi:
<> == Type31
<mor root> == y
<phon harmony> == front.

Sisar:
<> == Type32
<mor root> == sisar
<phon harmony> == back.

Tatar:
<> == Type32
<mor root> == ta Gradation_Res:<phon tt> ar
<phon harmony> == back.

Kytkin:
<> == Type33
<mor root> == kytki
<phon harmony> == front.

Laidun:
<> == Type33
<mor root> == lai Gradation_Res:<phon t> u
<phon harmony> == back.

Onneton:
<> == Type34
<mor root> == onne Gradation_Res:<phon tt> o
<phon harmony> == back.

Nimetön:
<> == Type34
<mor root> == nime Gradation_Res:<phon tt> ö
<phon harmony> == front.

Lämmin:
<> == Type35
<mor root> == lä Gradation_Res:<phon mp> i
<phon harmony> == front.

Hapan:
<> == Type35
<mor root> == ha Gradation_Res:<phon pp> a
<phon harmony> == back.

Sisin:
<> == Type36
<mor root> == sisi
<phon harmony> == front.

Pahin:
<> == Type36
<mor root> == pahi
<phon harmony> == back.

Vasen:
<> == Type37
<mor root> == vase
<phon harmony> == back.

Nainen:
<> == Type38
<mor root> == nai
<phon harmony> == back.

Vastaus:
<> == Type39
<mor root> == vastau
<phon harmony> == back.

Kalleus:
<> == Type40
<mor root> == kalleu
<phon harmony> == back.

Kauneus:
<> == Type40
<mor root> == kauneu
<phon harmony> == back.

Vieras:
<> == Type41
<mor root> == viera
<final vowel> == a
<phon harmony> == back.

Keidas:
<> == Type41
<mor root> == kei Gradation_Res:<phon t> a
<final vowel> == a
<phon harmony> == back.

Mies:
<> == Type42
<mor root> == mie
<phon harmony> == front.

Ohut:
<> == Type43
<mor root> == ohu
<phon harmony> == back.

Immyt:
<> == Type43
<mor root> == i Gradation_Res:<phon mp> y
<phon harmony> == front.

Kevät:
<> == Type44
<mor root> == kevä
<phon harmony> == front.

Kahdeksas:
<> == Type45
<mor root> == kahdeksa
<phon harmony> == back.

Kolmas:
<> == Type45
<mor root> == kolma
<phon harmony> == back.

Tuhat:
<> == Type46
<mor root> == tuha
<phon harmony> == back.

Kuollut:
<> == Type47
<mor root> == kuoll
<phon harmony> == back.

Hame:
<> == Type48
<mor root> == hame
<phon harmony> == back.

Sade:
<> == Type48
<mor root> == sa Gradation_Res:<phon t> e
<phon harmony> == back.

%% Currently not covered

Askel:
<> == Type49
<mor root> == askel
<phon harmony> == back.

Isoäiti:
<> == Type50
<mor root> == isoäiti
<phon harmony> == front.

Nuoripari:
<> == Type51
<mor root> == nuoripari
<phon harmony> == back.